import org.gradle.util.VersionNumber
if (VersionNumber.parse(extensionConfig.dependencies.rapidminer) >= VersionNumber.parse('9.0.0')) {

    configurations {
        i18n {
            description "jar files that contain i18n properties"
            transitive false
        }
    }

    dependencies {
        i18n group: 'com.rapidminer.studio', name: 'i18n-extraction', version: "(,$extensionConfig.dependencies.rapidminer]"
    }

    if (project.tasks.findByName('initializeExtensionProject') != null) {
        initializeExtensionProject.doLast {
            copyResources()
            //delete 'loadI18N.gradle'
        }
    }
}

def copyResources() {
    def langCode = language.replace(' ', '_').toLowerCase()
    def resourceDir = "src/main/resources/"
    def srcDir = "src/main/java/"
    def groupID = project.group.replace('.', '/')
    def extPackage = groupID + '/' + extensionConfig.namespace
    def extName = extensionConfig.name.replace(' ', '')
    configurations.i18n.forEach { i ->
        copy {
            from zipTree(i)
            into resourceDir
            rename '\\.properties', '_' + langCode + '.properties'
            include 'com/rapidminer/**/**.properties'
        }
        copy {
            from zipTree(i)
            into resourceDir + extPackage
            include 'language.content'
        }
        copy {
            from zipTree(i)
            into srcDir + extPackage
            include '*.java'
        }
    }

    fileTree(srcDir + extPackage).filter {f -> !f.name.contains('PluginInit')}. forEach { f ->
        f.write 'package ' + extPackage.replace('/', '.') + ';\n' + f.text.replaceAll('PluginInit', 'PluginInit' + extName)
    }
    def pluginInit = file(srcDir + extPackage + '/PluginInit' + extName +".java")
    pluginInit.write pluginInit.text.replace('initPlugin() {',
            'initPlugin() {\n\t\t\tBundleLoader.loadBundles("' + langCode + '");\n\t\t')
}